!> @copyright Copyright 2023 UChicago Argonne, LLC and contributors
!!
!! @licenseblock
!!   Licensed under the Apache License, Version 2.0 (the "License");
!!   you may not use this file except in compliance with the License.
!!
!!   Unless required by applicable law or agreed to in writing, software
!!   distributed under the License is distributed on an "AS IS" BASIS,
!!   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
!!   See the License for the specific language governing permissions and
!!   limitations under the License.
!! @endlicenseblock
!!
!! @file
!> @ingroup physics_Eos
!!
!! @brief Weaklib specific implementation of the API call for a computing
!!     equation of state on cells in a multidimensional array
!! 
!! @subref{Eos_multiDim}
!!
!!

! solnData depends on the ordering on unk
!!REORDER(4): solnData
!!NOVARIANTS

#include "Eos.h"
#include "Eos_map.h"
#include "constants.h"
#include "Simulation.h"
 
subroutine Eos_multiDim(mode,range,solnData)

  use Driver_interface, ONLY : Driver_abort
  use Logfile_interface, ONLY: Logfile_stampMessage 
  use eos_localInterface, ONLY : eos_weakLib
  use Eos_data, ONLY : eos_threadWithinBlock,eos_mapLookup, eos_smalle, eos_eintSwitch


#include "FortranLangFeatures.fh"

  integer, intent(in) :: mode
  integer, dimension(2,MDIM), intent(in) :: range
  real, POINTER_INTENT_IN :: solnData(:,:,:,:)

  integer :: ierr
  integer :: i,j,k, vLen1D, vecLen,offset
  integer,dimension(MDIM) :: pos

  integer :: n,m
  real :: @M eos_args
!!$  REAL, DIMENSION(:),allocatable :: xDens,xTemp,xYe,xEner,xPres,xEntr,xGamc,xAbar, xZbar,xekin
  @M eos_map_declare
  real, allocatable, dimension(:,:) :: eosData, massFrac

  @M eos_debug
  if (mode==MODE_EOS_NOP) return ! * Return immediately for MODE_EOS_NOP! *

  vLen1D = range(HIGH,IAXIS)-range(LOW,IAXIS)+1
  vecLen=vLen1D*(range(HIGH,JAXIS)-range(LOW,JAXIS)+1)*(range(HIGH,KAXIS)-range(LOW,KAXIS)+1)
  if (vecLen==0) return ! * Return immediately for empty IAXIS range! (for efficiency and avoiding index range errors)

  ! solnData points to solution data in UNK (or other data structure).
  ! The length of the data being operated upon is determined from the range input array.


  allocate(massFrac(NSPECIES,vecLen))
  allocate(eosData(vecLen, EOS_VARS))
  
  @M eos_map

  n = 0
  @M loop_3d(range)
             n=n+1
             massFrac(1:NSPECIES,n) = solnData(SPECIES_BEGIN:SPECIES_END,i,j,k)
             @M eos_preprocess
             if (ye_map /= NONEXISTENT) then
                eosData(n,EOS_YE) = solnData(ye_map,i,j,k)
             endif

             eosData(n, EOS_EINT) = internalEnergy
             eosData(n, EOS_ABAR) = abar
             eosData(n, EOS_ZBAR) = zbar
             eosData(n, EOS_ENTR) = entr
             eosData(n, EOS_PRES) = solnData(pres_map,i,j,k)
             eosData(n, EOS_DENS) = solnData(dens_map,i,j,k)
             eosData(n, EOS_TEMP) = solnData(temp_map,i,j,k)
             eosData(n, EOS_GAMC) = solnData(gamc_map,i,j,k)
             eosData(n, EOS_EKIN) = ekin
   @M loop_end_3d   
   call eos_weakLib(mode,vecLen,eosData, massFrac)
   n=0
   @M loop_3d(range)
             n=n+1
             solnData(pres_map,i,j,k) = eosData(n, EOS_PRES)
             solnData(temp_map,i,j,k) = eosData(n, EOS_TEMP)
             solnData(gamc_map,i,j,k) = eosData(n, EOS_GAMC)
             if(eint_map /= NONEXISTENT)solnData(eint_map,i,j,k) = eosData(n, EOS_EINT)
             if(ener_map /= NONEXISTENT)solnData(ener_map,i,j,k) = eosData(n, EOS_EINT)+eosData(n, EOS_EKIN) 
             if(entr_map /= NONEXISTENT)solnData(entr_map,i,j,k) = eosData(n, EOS_ENTR)
             
             solnData(game_map,i,j,k) = eosData(n,EOS_PRES)/&
                  (eosData(n, EOS_EINT) *eosData(n,EOS_DENS)) +1
             
  @M loop_end_3d    

        
  deallocate(massFrac)
  deallocate(eosData)

end subroutine Eos_multiDim
